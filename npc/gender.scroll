# Hexroll Scroll Model is open-source software released under the terms of the
# GPL Affero Public License v3.
#
# Copyright (C) 2024 Ithai Levi ithai@pendicepaper.com
#
Gender { ^ [ *(x50) Female
             *(x50) Male
             *(x01) Fluid ]
}

GenderFeature {
    HairColor! @ [ * red
                   * blonde
                   * black
                   * brown
                   * almond
                   * white ]

    HairLength! @ [ * long
                    * short ]

}

MasculineFeature (GenderFeature) {
    | GenderFeature
    Feature! @ [ *(x3) a {{HairLength}} {{HairColor}} beard
                 *(x3) a heavy {{HairColor}} beard
                 *(x5) short {{HairColor}} hair
                 *(x5) thin {{HairColor}} hair
                 *(x1) no hair
                 *(x1) thick bushy eyebrows
                 *(x1) a strong, prominent chin
                 *(x1) a square jaw
                 *(x1) a two day stubble
                 *(x1) a scruff
                 *(x1) a thick moustache
                 *(x1) a goatee beard
                 *(x1) a frowny expression
                 *(x1) a long smoking pipe in his mouth
                 *(x1) a big round belly
                 *(x1) a deep, commanding voice
                 *(x1) a hoarse voice
                 *(x1) a hairy chest
                 *(x1) a chiseled chin
                 *(x1) thick sideburns
                 *(x1) patchy beard
                 *(x1) thin greying hair ]
}

FeminineFeature (GenderFeature) {
    | GenderFeature
    HairTrait @ [ * silky
                  * flowing
                  * curly
                  * wavy
                  * braided
                  * wild
                  * well-brushed ]
    Feature! @ [ *(x3) {{HairLength}} and {{HairTrait}} {{HairColor}} hair
                 *(x3) {{HairLength}} {{HairColor}} hair
                 *(x3) {{HairTrait}} {{HairColor}} hair
                 *(x3) {{HairLength}} {{HairColor}} pony-tail
                 *(x3) soft gentle features
                 *(x3) delicate features
                 *(x3) soft voice
                 *(x3) warm voice
                 *(x3) painted skin decorations
                 *(x3) delicate makeup
                 *(x3) heavy makeup
                 *(x2) full lips
                 *(x1) a beauty mark above the lips
                 *(x1) ample breasts
                 *(x1) ample hips ]
}

Male (Gender) {
    | Gender
    NameClass! = "NameMale"
    PronounSubject! =< he >
    PronounObject! =< him >
    PronounPossessive! =< his >
    Possession! =< has >
    BeVerb!     =< is >
    GenderFeature! @@ [
        *(x100) MasculineFeature
        *(x1)   FeminineFeature ]
    Feature! =< {{GenderFeature.Feature}} >
}

Female (Gender) {
    | Gender
    NameClass! = "NameFemale"
    PronounSubject! =< she >
    PronounObject! =< her >
    PronounPossessive! =< her >
    Possession! =< has >
    BeVerb! =< is >
    GenderFeature! @@ [
        *(x1)     MasculineFeature
        *(x100)   FeminineFeature ]
    Feature! =< {{GenderFeature.Feature}} >
}

Fluid (Gender) {
    | Gender
    NameClass! = "NameFluid"
    PronounSubject! =< they >
    PronounObject! =< them >
    PronounPossessive! =< their >
    Possession! =< have >
    BeVerb! =< are >
    GenderFeature! @@ [
        *(x1) MasculineFeature
        *(x1) FeminineFeature ]
    Feature! =< {{GenderFeature.Feature}} >
}
