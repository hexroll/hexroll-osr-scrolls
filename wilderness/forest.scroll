# Hexroll Scroll Model is open-source software released under the terms of the
# GPL Affero Public License v3.
#
# Copyright (C) 2024 Ithai Levi ithai@pendicepaper.com
#
Forest {
  Name! = Forest
  Statue @ [
    * warrior
    * king
    * queen
  ]
  Skeleton @ [
    * dragon, as if sleeping peacefully
    * giant humanoid, lying face down
  ]
  Supplemental! @ [
    * Giant red, purple and blue mushrooms grow beside the trees here, their
      colors are vibrant and bright, much like gemstones.

    * The trees here are naked and twisted. Their branches reach down like bony
      claws. Was this place cursed in days long gone?

    * So big and tall are the trees here. Their tops are almost hidden by the
      dense mist.  Their trunks are as wide as a hut. Some of them have planks
      of wood attached, forming a ladder.

    * A huge, lush tree is standing in a clearing here, covered with colourful
      flowers and holding a sweet berry inside. Birds fly cheerfully around it.

    * Moss covered ruins of an ancient stone structure emerge from within the
      vegetation. Carvings of spirals and swirls decorate some of the larger
      stones.

    * The forest is dense and dark here, but countless clusters of
      phosphorescent fungi are growing near the base of the trunks, creating a
      luminous colourful glow above the ground.

    * Dense ivy and vegetation cover an old skeleton of a {{Skeleton}}
      in a clearing surrounded by the trees.

    * Ropes tangle down from the branches here. On some of them, human-sized
      straw-made figures are hanging by their canvas covered heads.

    * Dozens of giant dead tree trunks are laying on the ground here, covered
      with moss and undergrowth. Some of them seem to have a face, with a
      frozen expression of agony.

    * An oversized statue of {{articlize(Statue)}} holding a sword
      against the ground stands proudly in a clearing here.
      Fresh flowers were recently placed near its base.

    * There's a stream of clear water here, flowing gently between the woods,
      over the smooth rock ground.

    * Colourful flowers, berry shrubs and mushrooms grow under the woods
      here, dense enough to almost entirely cover a large number of human-like bones.

    * A small pond sits between the woods here. At night, countless
      mysterious forms glow an eerie purple light deep in the water.

  ]
  Location! @ [
    * near a water stream
    * next to a clearing
    * deep in the woods
    * where the woods meet a large hill
  ]
  Hideout! @ [
    * under the ruins of an ancient city
    * in one of the tree covered hills
    * near an abandoned stronghold, deep in the woods
  ]
}

ForestRandomEncounterTable {
  [4..4 OtherEncounters!] @ ForestRandomEncounter
}

ForestHex (Hex) {
  Terrain! @ Forest
  Monster! @ ForestFeatureEncounter
  RandomEncounters @ ForestRandomEncounterTable
  ExtensionTypeClass = "ForestHexExtension"
  | Hex
}

ForestRegion (Region) {
  Suffix @ [
    * Forest
    * Woods
    * Woodlands
    * Timberlands
    * Grove
  ]
  | Region
  Weather? @ NormalWeatherContainer
  [$minimum_tiles_per_forest_region..$maximum_tiles_per_forest_region Hexmap?] @ ForestHex
}

